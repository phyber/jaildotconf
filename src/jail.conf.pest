// Helpers for other fragments.
any_inc_eol = _{ NEWLINE | ANY }
any_not_eol = _{ !NEWLINE ~ ANY }
quotes = _{ "\"" | "\'" }
whitespace = _{ " "+ | "\t"+ }

// /*
//  * C style /* comment */
//  */
c_comment = { "/*" ~ (c_comment | !"*/" ~ ANY)* ~ "*/" }

// CPP style comment
cpp_comment = { "//" ~ any_not_eol* }

// # Shell style comment
shell_comment = { "#" ~ any_not_eol* }

// Values
// allow.mount = true;
// allow.mount = "true";
// allow.mount = 'true';
// "allow.mount" = true;
// "allow.mount" = "true";
// "allow.mount" = 'true';
// 'allow.mount' = true;
// 'allow.mount' = "true";
// 'allow.mount' = 'true';
// Escaped quotes within an already quoted string are currently rejected.
param_name_base = _{ (!(whitespace | quotes | "+" | "=" | ";") ~ ANY)+ }
param_name_double_quoted = _{ "\"" ~ (!"\"" ~ param_name_base)* ~ "\"" }
param_name_single_quoted = _{ "\'" ~ (!"\'" ~ param_name_base)* ~ "\'" }
param_name_unquoted = _{ !quotes ~ param_name_base }
param_name = { param_name_double_quoted | param_name_single_quoted | param_name_unquoted }
param_additive = { "+"? }
param_assignment = { param_additive ~ "=" }
param_value_double_quoted = _{ "\"" ~ (!("\"" | ";") ~ ANY)* ~ "\"" }
param_value_single_quoted = _{ "\'" ~ (!("\'" | ";") ~ ANY)* ~ "\'" }
param_value_unquoted = _{ (!(whitespace | quotes | ";") ~ ANY)* }
param_value = { param_value_double_quoted | param_value_single_quoted | param_value_unquoted }
param_with_value = {
    param_name
    ~ whitespace?
    ~ param_assignment
    ~ whitespace?
    ~ param_value
    ~ whitespace?
    ~ ";"
}
param_without_value = { param_name ~ whitespace? ~ ";" }
