// Helpers for other fragments.
any_inc_eol = { NEWLINE | ANY }
any_not_eol = { !NEWLINE ~ ANY }
quotes = { "\"" | "\'" }
whitespace = { " "+ | "\t"+ }

// /*
//  * C style /* comment */
//  */
c_comment = { "/*" ~ (c_comment | !"*/" ~ ANY)* ~ "*/" }

// CPP style comment
cpp_comment = { "//" ~ any_not_eol* }

// # Shell style comment
shell_comment = { "#" ~ any_not_eol* }

// Values
// allow.mount = true;
// allow.mount = "true";
// allow.mount = 'true';
// "allow.mount" = true;
// "allow.mount" = "true";
// "allow.mount" = 'true';
// 'allow.mount' = true;
// 'allow.mount' = "true";
// 'allow.mount' = 'true';
// Escaped quotes within an already quoted string are currently rejected.
param_name_base = { (!(whitespace | quotes | "+" | "=" | ";") ~ ANY)+ }
param_name_double_quoted = { "\"" ~ (!"\"" ~ param_name_base)* ~ "\"" }
param_name_single_quoted = { "\'" ~ (!"\'" ~ param_name_base)* ~ "\'" }
param_name_unquoted = { !quotes ~ param_name_base }
param_name = { param_name_double_quoted | param_name_single_quoted | param_name_unquoted }
param_additive = { "+"? }
param_assignment = { param_additive ~ "=" }
param_value_double_quoted = { "\"" ~ (!("\"" | ";") ~ ANY)* ~ "\"" }
param_value_single_quoted = { "\'" ~ (!("\'" | ";") ~ ANY)* ~ "\'" }
param_value_unquoted = { (!(whitespace | quotes | ";") ~ ANY)* }
param_value = { param_value_double_quoted | param_value_single_quoted | param_value_unquoted }
param_with_value = {
    param_name
    ~ whitespace?
    ~ param_assignment
    ~ whitespace?
    ~ param_value
    ~ whitespace?
    ~ ";"
}
param_without_value = { param_name ~ whitespace? ~ ";" }
